import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.jetbrains.kotlin.jvm' version '1.9.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.21'
	id 'com.google.protobuf' version "0.9.4"
	id 'org.jetbrains.kotlin.plugin.allopen' version "1.9.21"
}

group = 'com.apani'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'software.amazon.awssdk:s3:2.20.136'

	// gRPC dependencies
	implementation 'io.grpc:grpc-kotlin-stub:1.4.0'
	implementation 'io.grpc:grpc-netty-shaded:1.62.2'
	implementation 'io.grpc:grpc-protobuf:1.62.2'
	implementation 'io.grpc:grpc-stub:1.62.2'
	implementation 'com.google.protobuf:protobuf-java:4.28.2'
	implementation 'com.google.protobuf:protobuf-kotlin:4.28.2'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.25.1"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.62.2"
		}
		grpckt {
			artifact = "io.grpc:protoc-gen-grpc-kotlin:1.4.0:jdk8@jar"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc {}
				grpckt {}
			}
			// Add Kotlin code generation
			task.builtins {
				java {}
				kotlin {}
			}
		}
	}
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/proto'
		}
		java {
			srcDirs += [
					'build/generated/source/proto/main/java',
					'build/generated/source/proto/main/grpc',
					'build/generated/source/proto/main/grpckt'
			]
		}
		kotlin {
			srcDirs += [
					'build/generated/source/proto/main/kotlin',
					'build/generated/source/proto/main/grpckt'
			]
		}
	}
}

// Fix for duplicate proto files issue
tasks.withType(Copy).configureEach {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Configure Kotlin compiler
tasks.withType(KotlinCompile).configureEach {
	kotlinOptions {
		freeCompilerArgs += "-Xjsr305=strict"
		jvmTarget = "17"
	}
	// Make sure protobuf generation happens before Kotlin compilation
	dependsOn 'generateProto'
}

tasks.named('test') {
	useJUnitPlatform()
}
